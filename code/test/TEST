; runs the whole series of tests for GIFA
; uses minimum constructs
;
print "Test : basic"
; sets the timer on to know how much it takes
set time = (1 == 1)   ; set time to true for timing
if ($time) timer 1
basic
if ($error != 0) error "Gifa is in bad state, can't proceed"
print "----------Ok"
if ($time) timer 1

onerrorgoto error
set failed = " "
set succeed = " "
set crashed = " "
set total = 0
set cmd = open
open TEST_LIST
=loop
        set cmd = <TEST_LIST
        if  (eof("TEST_LIST")) goto fin
        print ("*********** Test : " // $cmd )
        set error := 0
        $cmd
        if ($error == 0) print "----------Ok"
        if ($error != 0) print "----------Failed"
        if ($error != 0) set failed = ($failed ; $cmd)
        if ($time) timer 1
        if ($error == 0) then
              set t = ( int(head(tail($timer))*100)/100 )
              set succeed = ($succeed ; $cmd ; "("; $t; "sec.)" )
              set total = ($total+$t)
        endif
        goto loop
=fin
print ' '
print ' '
print ' '
print '********************************************************'
print '                    Test Results'
print '********************************************************'
if ($failed s= " " & $crashed s= " ") then
        print "All tests successfully finished"
        print $succeed
        print ("Total execution time :";$total)
else
	print "Tests Succeded :"
	print $succeed
	print "Test failed :"
	print $failed
	print "Test crashed :"
	print $crashed
endif
print '********************************************************'
print ' '
print ' '
refmacro 0
if ($time) timer 0
exit

=error
print ("Test"; $cmd; "CRASHED !")
set crashed = ($crashed ; $cmd)
onerrorgoto error
goto loop

