; dosy2d data_type processing_type size thres 
;
; implements the complete DOSY processing for 2D
;
; DOSY data are in-memory, diffusion dimension is in F1,
; NMR spectra in F2
;
; data_type :       tabulated or regular   determines the type of sampling
; processing_type : MaxEnt, Fit or Fit_2_comp   determines the processing which will be used
;           Fit is faster (if you have a small number of data-point), but is less accurate,
;           and cannot handle supersposition in spectra, nor polydisperse species.
;           Fit_2_comp tries to fit two exponential components
;             should be used with CAUTION
; size : size after Laplace inversion
; thres : multiplier used to determine the processing threshold
;         only columns which first point is larger
;         than the noise level by this ratio, will be processed
;
; see also :  INVLAP, INVTLAP, FITGENE, dosyfit, dosyfit_2, dosy3d, dosy2d.g, dosy_setup

gm/test2d
if ($noise == 0.0) error 'NOISE should not be 0.0'

message "Enter data type (regular/tabulated)"
set type = $_
set type = (toupper($type))
if ($type s! "REGULAR" & $type s! "TABULATED")   error "Wrong data type"
if ($type s= "TABULATED" & $si_tab != $si1_2d) \
    error "F1 dimension not compatible with TAB buffer"

message "Enter processing type (Fit / Fit_2_comp / MaxEnt)"
set proc = $_
set proc = (toupper($proc))
if ($proc s! "FIT" & $proc s! "MAXENT" & $proc s! 'FIT_2_COMP') then
   error "Wrong processing type"
endif

;; preset peak table (used to construct pseudo peaks)
;dim 1 pkclear point_input 1 point->pk
;setpeak 1 1000 ($si1_1d/2) 3

message "Enter final size"
set sz = $_
if ($sz < 4) error "Size too small"
if ($sz > $MEM_PRO_1D) error "Size too large"

message "Enter noise factor used for thresholding"
set thresh = $_

; compute sizes to be used before and after
set szbf = $si1_2d

if ($sz > $szbf) then
        dim 2 chsize $sz %
        set szaf = $sz
else
        set szaf = $szbf
endif

; fake a regular mode in FIT
if (($PROC s= 'FIT' | $proc s= 'FIT_2_COMP') & $type s= 'REGULAR') then
  dim 2 col 1 dim 1
  one tm $si1_1d $si1_1d addbase (1/$si1_1d)
  mult (0.5/$SPECW_1D)
  put tab
endif

; output file
set result = 'result_dosy.temp'

; Count the number of columns to process
dim 2 row 1 dim 1
count ($thresh * $noise)
set peakcount = $returned
initinprogress (($peakcount+1))
print ($peakcount;'columns are to process')
set peakcount = 1
inprogress $peakcount

; do the processing
unit i
dim 2 zoom 0 scale 1 col 1
open $result
; initial value of P1 is set once, only
set p1 := ($dfactor/$tab[round($si_tab/2)]) ; eval initial $p1 and $p2
for i = 1 to $si2_2d
        if (val2d(1,$i) > $thresh * $noise) then
           set peakcount = (%+1)
           inprogress $peakcount
           col $i dim 1
           print ("Processing col " // $i // "... ")
           chsize $szbf
           if ($proc s= 'MAXENT') then
              if ($type s= "TABULATED") then
                 maxent_dosy $sz
                 fprint $result ("col";$i;"iter. count";$nbreiter;"chi2";$chi2min)
              else
                 invlap $sz
              endif
           else
              set exp = '$p2*exp(-$p1*$x/$dfactor)'
              set p1 := ($dfactor/$tab[round($si1_1d/2)])
              set p2 := (val1d(1))              ; eval initial $p1 and $p2
              fitgene $exp 2			; do first fit with 1 damping
              fprint $result ("col";$i;"chi2_1_comp";$chi2)
              set p1_1 = $p1
              set p2_1 = $p2
              set dp1_1 = $dp1
              set dp2_1 = $dp2
              set chi2_1 = $chi2 
              if ($proc s= 'FIT_2_COMP' & $chi2 > ($si1_1d)) then
                 set exp = ($exp // '+$p4*exp(-$p3*$x/$dfactor)')
                 set p2 = ($p2_1/2)   set p4 = $p2
                 set p3 = ($p1_1/3)   set p1 = (3*$p1_1)
                 fitgene $exp 4			; then final fit
              else
                 set p3 = 0.0
              endif
              if ( $chi2 < (0.8*$chi2_1) & $p1>0 & $p3>0 ) then
                 chsize $szaf
                 fprint $result ("         chi2_2_comp";$chi2)
                 fprint $result ('         damping 1';$p1;'+/-';$dp1;'amplitude 1';$p2;'+/-';$dp2)
                 fprint $result ('         damping 2';$p3;'+/-';$dp3;'amplitude 2';$p4;'+/-';$dp4)
                 set left1 = (dtoi(max(0,$p1-$dp1),1,1))
                 set right1 = (dtoi($p1+$dp1,1,1))
                 set left2 = (dtoi(max(0,$p3-$dp3),1,1))
                 set right2 = (dtoi($p3+$dp3,1,1))
                 chsize (%*2)
                 zero 
                 if ($p1 < $dmin | $p1 > $dmax) \
                      print "Warning, outside damping limits"
                 if ($p3 < $dmin | $p3 > $dmax) \
                      print "Warning, outside damping limits"
                 simun $p2 (dtoi($p1,1,1)) 0 0
                 gm (2*($right1-$left1)*$SPECW_1D/$SI1_1D)  
                 put data 
                 zero 
                 simun $p4 (dtoi($p3,1,1)) 0 0
                 gm (2*($right2-$left2)*$SPECW_1D/$SI1_1D)  
                 adddata
                 sin 0
                 ft
                 refmacro 1
                 real
                 refmacro 0
              else
                 fprint $result ('         damping';$p1_1;'+/-';$dp1_1;'amplitu';$p2_1;'+/-';$dp2_1)
                 if ($p1_1 <0) then
                    fprint $result 'negative damping, setting to zero'
                    dim 1 chsize $szaf zero
                 else
                    if ($p1_1 < $dmin | $p1_1 > $dmax) \
                      print "Warning, outside damping limits"
                    chsize $szaf
                    set left = (dtoi(max(0,$p1_1-$dp1_1),1,1))
                    set right = (dtoi($p1_1+$dp1_1,1,1))
                    chsize (%*2)
                    zero simun $p2_1 (dtoi($p1_1,1,1)) 0 0
                    gm (2*($right-$left)*$SPECW_1D/$SI1_1D)  sin 0
                    ft
                    refmacro 1
                    real
                    refmacro 0
                 endif
              endif
           endif
           chsize $szaf
        else
           dim 1 chsize $szaf zero
        endif
        dim 2 put col $i
endfor
fprint $result (($peakcount-1);'columns processed')
close $result
dim 2
refmacro 0
print (($peakcount-1);'columns processed')
chsize $sz %
