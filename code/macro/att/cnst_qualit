; cnst_qualit (interactive)
;
; permits the output of a constraint file according to the set of 
; calibration peaks. The constraint distances are qualitatively
; defined from the calibration peaks, and the bounds given by the 
; user.
;
;
;
;


Print "Current project :"
pwd

if ($att['largest'] == 0) error "Database Empty !"
if ($dim != 2) error "Command available in 2D only !"
if (!(exist('calib_dst[]'))) error "You need first to define calibration peaks!"

if (!$arg) then
  formbox 'Qualitative constraint output' \
   'cnst_qualit $cnst_format $cnstfil $dstbg $dstmed $dbgl $dbgu $dmel $dmeu $dsml $dsmu $pseudo $intra' \
   'Constraint File Format' enum 'XPLOR,DYANA' cnst_format 'DYANA' \
   noreturn 'basename' file cnstfil cnst \
   'Large intensity bounds: lower' real dbgl 1.8 noreturn \ 
   'upper' real dbgu 2.8 \
   noreturn 'Lower limit' real dstbg 2.5 \
   'Medium intensity bounds: lower' real dmel 1.8 noreturn \ 
   'upper' real dmeu 3.6 \
   noreturn 'Lower limit' real dstmed 3.4 \
   'Small intensity bounds: lower' real dsml 1.8 noreturn \  
   'upper' real dsmu 4.4 \
   'use pseudo atom corrections' enum 'yes,no' pseudo 'yes' \
   'outout of intra residue constraints' enum 'none,only HN,all' intra 'none' \
   *

else
  set cnst_format = $_
  set endfil = $_
  set dstbg = $_ 
  set dstmed = $_ 
  set dbgl = $_ 
  set dbgu = $_ 
  set dmel = $_ 
  set dmeu = $_
  set dsml = $_ 
  set dsmu = $_
  set pseudo = $_
  set intra = $_

; set up of pseudo atom corrections
set m_corr = 1.0    ; methylene or methyl
set q_corr = 2.4    ; double methyl or aromatics
; list of larger pseudo-atom corrections
set res_q = 'VAL:QG LEU:QD PHE:QD PHE:QE TYR:QD TYR:QE'
; list of methyls
;avec methylene set res_m = 'ALA:HB THR:HG ILE:HG ILE:HD VAL:HG LEU:HD MET:HE GLY:QA SER:QB CYS:QB ASP:QB ASN:QB LEU:QB ILE:QG LYS:QB LYS:QG LYS:QD LYS:QE LYS:QZ ARG:QB ARG:QG ARG:QD ARG:QH1 ARG:QH2 ARG:QH GLU:QB GLU:QG GLN:QB GLN:QG GLN:QE MET:QB MET:QG PRO:QB PRO:QG PRO:QD HIS:QB PHE:QB TYR:QB TRP:QB'

set res_m = 'ALA:HB THR:HG ILE:HG ILE:HD VAL:HG1 VAL:HG2 LEU:HD1 LEU:HD2 MET:HE'
;lookup for aromatic spin systems
set arom['Arom-PHE'] = 'PHE'
set arom['Arom-TYR'] = 'TYR'
set arom['Arom-HIS'] = 'HIS'
set arom['Arom-TRP'] = 'TRP'

; correspondance codage entre GIFA et DYANA

set g2d["ALA:HB"] = "ALA:QB"
set g2d["THR:HG"] = "THR:QG2"
set g2d["MET:HE"] = "MET:QE"
set g2d["VAL:QG"] = "VAL:QQG"
set g2d["VAL:HG1"] = "VAL:QG1"
set g2d["VAL:HG2"] = "VAL:QG2"
set g2d["LEU:HD1"] = "LEU:QD1"
set g2d["LEU:HD2"] = "LEU:QD2"
set g2d["LEU:QD"] = "LEU:QQD"
set g2d["ILE:HG"] = "ILE:QG2"
set g2d["ILE:QG"] = "ILE:QG1"
set g2d["ILE:HD"] = "ILE:QD1"
set g2d["GLN:QE"] = "GLN:QE2"
set g2d["ARG:QH"] = "ARG:QH2"
set g2d["ASN:QD"] = "ASN:QD2"
set g2d["GLY:HA2"] = "GLY:HA1"
set g2d["GLY:HA3"] = "GLY:HA2"
set g2d["LYS:HZ"] = "LYS:QZ"
; correspondance dans les cas de renommage (au vol) a partir des pseudo
; necessaire apres modifications automatiques
set g2d["VAL:HG2"] = "VAL:QG1"
set g2d["VAL:HG3"] = "VAL:QG2"
set g2d["LEU:HD2"] = "LEU:QD1"
set g2d["LEU:HD3"] = "LEU:QD2"
set g2d["GLN:HE2"] = "GLN:HE21"
set g2d["GLN:HE3"] = "GLN:HE22"





; computation of the factor between intensity and (1/d**6) by
; a least-square
  set sumi2 = 0
  set sumij = 0
  foreach i in calib_dst
    if ($calib_dst[$i] == 0) error ("Pb with calibration distance of peak" ; $i)
    set l = $att[$i]
    set t = (head(tail(tail($l))))
;   print ("t " ; $t ; $calib_dst[$i] ; $i)
    set sumi2 = (%+($t*$t))
    set sumij = (%+($t/($calib_dst[$i]^6)))
  endfor
  if ($sumi2 == 0) error "Pb with calibration intensities!" 
  set factor = ($sumij/$sumi2)
; determine the intensity threshold for which the corresponding distance is 
; larger than 5.0, this value is used to reject the constraints corresponding to 
; too small intensities
  set thres = ($factor/(5.0^6))

  if ($cnst_format s= "XPLOR") then
    	set endfil = ("constraints/" // % //".tbl")
  elsif ($cnst_format s= "DYANA") then
    	set endfil = ("constraints/" // % //".upl")
  endif
  set cnstfil = 'constraints/temporary'
  open $cnstfil

; test
; set serqb_serqb = 0
; set noe_diff = 0




initinprogress $att["LARGEST"]
dbopen hash hash
set uu = 0
set ncp = 0
set nbhash = 0
print "Collecting Peaks Informations..."
foreach i in att
    if ($i s= "LARGEST")  goto break
      inprogress $uu
      set uu = (%+1)


      ; get the assigned peak label
      set l = (toupper($att[$i]))
      if (index($l,'UNK') != 0) goto break     ; processing of the assigned peaks


      set pk_coor_f1 = (head($l))
      set pk_coor_f2 = (head(tail($l)))
      set t = (tail(tail(tail($l))))
      set sp1 = (head($t))
      set sp2 = (head(tail($t)))
      if ($sp1 s= $sp2) goto break 
      set s1 = ($spin[$sp1])
      set s2 = ($spin[$sp2])
      if ((index(toupper($s1),'UNK') != 0)  | (index(toupper($s2),'UNK') != 0) ) goto break  ;and of named spins


      set sys1 = (head(tail(tail($s1))))
      set sys2 = (head(tail(tail($s2))))
      set r1 = (head($sys[$sys1]))		;r1, r2 : system primary index
      set r2 = (head($sys[$sys2]))
      if ((toupper($r1) s= "UNK") | (toupper($r2) s= "UNK")) goto break    ; in assigned systems

      ; clean up the base 
      set a1 = (head(tail($s1)))		;a1, a2: spin name
      set a2 = (head(tail($s2)))
      if ($a1 s= $a2 & $r1 == $r2) goto break   ; case of  SER-QB  with SER-QB

      if ($sys1 == $sys2) then     ; case of intra residue
         if ($intra s= 'none') then 
           goto break
         elsif ($intra s= 'only HN') then
           if ($a1 s! 'HN' & $a2 s! 'HN') goto break
         endif
      endif

      ; get the peak intensity
      set u = (head(tail(tail($l))))
      if ($u < $thres) goto break
      set p = (1/($u*$factor))
      set distmoy = ($p^(1/6))
      set distmoy = (int($distmoy*100)/100)
      if ($distmoy < $dstbg) then        ; Big intensity interval
         	set varlow = $dbgl
         	set varupp = ($dbgu-$dbgl)
      elsif ($distmoy < $dstmed) then    ; Medium intensity interval
         	set varlow = $dmel
         	set varupp = ($dmeu-$dmel)
      else                               ; Small intensity interval
         	set varlow = $dsml
         	set varupp = ($dsmu-$dsml)
      endif



      ;localisation preferee: aliphatiques en dessous de la diagonale, les autres en dessus
      set loc = "none"
      if (($pk_coor_f2 < 4.7) & ($pk_coor_f1 < 4.7) & ($pk_coor_f2 <= $pk_coor_f1)) set loc = "ok"
      if (($pk_coor_f2 > 4.7) & ($pk_coor_f1 <= $pk_coor_f2)) set loc = "ok"

      if ( exist("hash[" // max($sp1,$sp2) // '-' // min($sp1,$sp2) // ']')) then
      		;localisation / a la diagonale sur le spectre
      		if ( $loc s= "ok" ) then   
			set hash[max($sp1,$sp2)//'-'//min($sp1,$sp2)] = ($sp1;$sp2;$varlow;$varupp;$i)
		endif
      else
      		; stockage
      		set hash[max($sp1,$sp2)//'-'//min($sp1,$sp2)] = ($sp1;$sp2;$varlow;$varupp;$i)
      		set nbhash = (%+1)
      endif



      ; stockage des noes avec H du meme methylene ou avec 2 methyl (val,leu)
      if ((index($a1,'Q') == 1) | (index($a2,'Q') == 1)) then
		set flag = 0
	    	if ( exist("hash[" // $r1 // ':' // $a1 // ':' // $r2 // ':' // $a2 // ']') ) then ; meme noe 
			set t = $hash[$r1//':'//$a1//':'//$r2//':'//$a2]
			set pflag = (head($t))
                        set psp1 = (head(tail($t))) 		; previous entry spin1 
                        set psp2 = (head(tail(tail($t)))) 	; previous entry spin2
		        if ((max($sp1,$sp2) != max($psp1,$psp2)) | (min($sp1,$sp2) != min($psp1,$psp2)) ) then
			 if (($pflag == 0)) then ; stockage eg: [HN:18:QB:17] = (sp_HN;sp_QB1;sp_QB2)
			   set flag = 1
			   if ($sp1 == $psp1) then
				set hash[$r1//':'//$a1//':'//$r2//':'//$a2] = ($flag;$psp1;$psp2;$sp2)
			   elsif ($sp1 == $psp2) then
                                set hash[$r1//':'//$a1//':'//$r2//':'//$a2] = ($flag;$psp2;$psp1;$sp2)
                           elsif ($sp2 == $psp1) then
                                set hash[$r1//':'//$a1//':'//$r2//':'//$a2] = ($flag;$psp1;$psp2;$sp1)
                           elsif ($sp2 == $psp2) then
                                set hash[$r1//':'//$a1//':'//$r2//':'//$a2] = ($flag;$psp2;$psp1;$sp1)
			   endif
			 endif
                        endif
		else
			set flag = 0 
			set hash[$r1//':'//$a1//':'//$r2//':'//$a2] = ($flag;$sp1;$sp2)
	        endif
      endif

=break
;endif ;TEST
endfor
inprogress $att["LARGEST"]


print " Computing Informations ..."
set cpt = 0
initinprogress $nbhash
foreach i in hash 


   if (index($i,':') == 0) then

;print ($i;"-->";$hash[$i])

       ; recup val
       set sp1 = (head($hash[$i]))
       set sp2 = (head(tail($hash[$i])))
       set varlow = (head(tail(tail($hash[$i]))))
       set varupp = (head(tail(tail(tail($hash[$i])))))
       set pk = (head(tail(tail(tail(tail($hash[$i]))))))
       set go_out = 0
       set s1 = ($spin[$sp1])
       set s2 = ($spin[$sp2])
       set sys1 = (head(tail(tail($s1))))
       set sys2 = (head(tail(tail($s2))))
       set r1 = (head($sys[$sys1]))              ;r1, r2 : system primary index
       set r2 = (head($sys[$sys2]))
       set a1 = (head(tail($s1)))                ;a1, a2: spin name
       set a2 = (head(tail($s2)))
       set nr1 = (head(tail($sys[$sys1])))       ; get syst names
       set nr2 = (head(tail($sys[$sys2])))
       if (index($nr1,'-') != 0)  set nr1 = $arom[$nr1]    ; clean aromatic syst names
       if (index($nr2,'-') != 0)  set nr2 = $arom[$nr2]


	; test des noes sur double H de methylene
	if ( exist("hash[" // $r1 // ':' // $a1 // ':' // $r2 // ':' // $a2 // ']')) then
           set t = $hash[$r1//':'//$a1//':'//$r2//':'//$a2]
;print "exist"
;print ($r1//':'//$a1//':'//$r2//':'//$a2;">>>";$t)
	   set double = (head($t))
	   if ($double == 1) then 	; double noes
;print " double"
                set psp1 = (head(tail($t)))
                set psp2 = (head(tail(tail($t))))	; proton du methylene
       		set psp3 = (head(tail(tail(tail($t))))) ; proton du methylene
		set n1 = $hash[max($psp1,$psp2)//'-'//min($psp1,$psp2)]
		set varlow1 = (head(tail(tail($n1))))
       		set varupp1 = (head(tail(tail(tail($n1)))))
		set n2 = $hash[max($psp1,$psp3)//'-'//min($psp1,$psp3)]
		set varlow2 = (head(tail(tail($n2))))
                set varupp2 = (head(tail(tail(tail($n2)))))

;print ($varlow1;$varlow2;$varupp1;$varupp2)

		if (($varlow1 == $varlow2) & ($varupp1 == $varupp2)) then ; meme intensite, pas de correction
									  ; changement de nom
;print "meme intensite"

			if ($psp1 == $sp1) then 		; psp1 n'est pas 1 H du methylene
				set label = (subst($a2,2,2))
				set suff1 = "2"
                                set suff2 = "3"
                                if ($sp2 == $psp2) set name = ("H"//$label//$suff1)
				if ($sp2 == $psp3) set name = ("H"//$label//$suff2)
                                set a2 = $name
				;print "a2"
				;print $name
			else
				set label = (subst($a1,2,2))
                                set suff1 = "2"
                                set suff2 = "3"
				if ($sp1 == $psp2) set name = ("H"//$label//$suff1)
				if ($sp1 == $psp3) set name = ("H"//$label//$suff2)
				set a1 = $name
				;print "a1"
				;print $name
			endif
	

		endif
		set tt1 = ($varlow1+$varupp1)
		set tt2 = ($varlow2+$varupp2)
			; noe courant, contrainte la moins forte : brk

		if ((max($sp1,$sp2) == max($psp1,$psp2)) & (min($sp1,$sp2) == min($psp1,$psp2)) & ($tt1 > $tt2)) goto brk
		if ((max($sp1,$sp2) == max($psp1,$psp3)) & (min($sp1,$sp2) == min($psp1,$psp3)) & ($tt1 < $tt2)) goto brk
;print "pas de brk"
	   endif
	endif



      if ($pseudo s= 'yes') then
        	if (index($a1,'Q') == 1) then
			;print "pseudo a1"
          		if (index($res_q,$nr1 // ':' // $a1) != 0) then ;if larger correction needed
            			set varupp = ($varupp+$q_corr)
          		else
            			set varupp = ($varupp+$m_corr)
          		endif
      		elsif (index($res_m,$nr1 // ':' // $a1) != 0)  then ; if methyl
          		set varupp = ($varupp+$m_corr)
      		endif
      		if (index($a2,'Q') == 1) then
			;print "pseudo a2"
          		if (index($res_q,$nr2 // ':' // $a2) != 0)  then   ; if larger correction needed
            			set varupp = ($varupp+$q_corr)
          		else
            			set varupp = ($varupp+$m_corr)
          		endif
        	elsif (index($res_m,$nr2 // ':' // $a2) != 0)  then ; if methyl
          		set varupp = ($varupp+$m_corr)
        	endif
      endif

      ;show_att $pk ; test
      if ($cnst_format s= "XPLOR") then
         fprint $cnstfil ('assign (resid';$r1;'and  name';$a1;')(resid';$r2;'and  name';$a2;')';$varlow;'0';$varupp)
      elsif ($cnst_format s= "DYANA") then

	 ; correspondance Gifa -> Dyana
	 set l = ($nr1 // ':' // $a1)
;print $l
	 if ( exist("g2d["//$l//"]") ) set a1 = (tailx($g2d[$l],":"))
;print $a1
	 set l = ($nr2 // ':' // $a2)
;print $l
	 if ( exist("g2d["//$l//"]") ) set a2 = (tailx($g2d[$l],":"))
;print $a2
         fprintf $cnstfil "%-3d %3s  %4s  %-3d %3s  %4s   %5.2f" $r1 $nr1 $a1 $r2 $nr2 $a2 ($varlow+$varupp) *
      endif
      set ncp = (%+1)
      set cpt = (%+1)
=brk
  endif
  inprogress $cpt
  endfor
  close $cnstfil
  inprogress $nbhash

  dbclose hash
  sh '/bin/rm hash.dir hash.pag'

  sh ('sort -n'; $cnstfil; '>'; $endfil)
  sh ('/bin/rm';$cnstfil)

  alert ("Constraint file :"; $endfil; "written;"; $ncp;"constraints")


;print $serqb_serqb ;test
;print $noe_diff ; test
endif

