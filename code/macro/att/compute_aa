; compute_aa $nopk
;
; Determine the aa number of a peak when this peak 
; is assigned 
; if the peak is homoresidue, issue one value
; issue i-j otherwise
;
; compute_aa is called by mult_integ
;

if (!$arg) message "Peak number?"
set i = $_

set super = 0
; look at the label of the current peak
if (exist("att[" // $i //"]")) then
   set l = $att[$i]
else
   error "Unknown peak"
endif

set f1 = (head($l)) set f2 = (head(tail($l)))
set f1i = (ptoi($f1,2,1))
set f2i = (ptoi($f2,2,2))

;print ("l "//$l)
set t = (tail(tail(tail($l))))
set u = (tolower(head($t))) 
set v = (tolower(head(tail($t)))) 
if (($u s! "unk" ) & ($v s! "unk" )) then
   set s1 = ($spin[(head($t))])
   set s2 = ($spin[(head(tail($t)))])
   set sys1 = (head(tail(tail($s1))))
   set sys2 = (head(tail(tail($s2))))
; processing of the assigned peaks
   if (($sys1 s! "unk") & ($sys2 s! "unk")) then 
      set r1t = (head($sys[$sys1]))
      set r2t = (head($sys[$sys2]))
      if ($r1t != $r2t) then
        set t = ($r1t // '-' // $r2t)
      else
        set t = $r1t
      endif
   else 
      set super = 1
   endif
else
   set super = 1
endif

if ($super == 1) then
   return " "
else 
   return $t
endif

