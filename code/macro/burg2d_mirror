; burg2d_mirror axis offset new_size
;
; axis is F1 or F2,
; extend the FID up to new_size, using burg_mirror method
;
; see also : burg_mirror burg2d burg3d BURG

refmacro 0
if ($dim != 2) error "Available in 2D only"
set axis = F1
message "enter axis (F1 / F2)"
set axis = $_
set axis = (toupper($axis))
message "time 0 offset"
set offset = $_
if (int($offset) s! $offset | $offset < -1 ) error "offset should be an integer equal or greater than -1"
if ($axis s= 'F1') set nsz = (2*$si1_2d)
if ($axis s= 'F2') set nsz = (2*$si2_2d)
message "Enter new size"
set nsz = $_
if ($axis s= "F1") then
	if ($nsz <= $si1_2d) error "Wrong size"
	set osz = $si1_2d
	chsize $nsz %
	set ext = "col"
	set imax = $si2_2d
elsif ($axis s= "F2") then
	if ($nsz <= $si2_2d) error "Wrong size"
	set osz = $si2_2d
	chsize % $nsz
	set ext = "row"
	set imax = $si1_2d
else
	error "Wrong axis for LP"
endif
initinprogress $imax

for i = 1 to $imax
	$ext $i
	dim 1
	chsize $osz
	itype 1 burg_mirror $offset $nsz
	dim 2 put $ext $i
	inprogress $i
endfor
exit

