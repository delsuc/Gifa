#!/bin/csh
#to get directly GIFA file form the spectro
# modif MAD 29 july 1998  - added 3D
#  modif MAD 03-2001  - added unalias, added chmod -w
#set verbose
unalias cd
unalias ls

if ($#argv == 7 || $#argv == 9 ) then
   set localpath=$1
   set spect=$2
   set user=$3
   set disk=$4
   set exp=$5
   set expno=$6
   set conv=$7
else if ($#argv == 0) then
  echo -n 'destination :'
  set localpath=$<
  echo -n 'spectrometer : '
  set spect=$<
  echo -n 'user-name :    '
  set user=$<
  echo -n 'disk :     '
  set disk=$<
  echo -n 'exp-name :     '
  set exp=$<
  echo -n 'exp-no :     '
  set expno=$<
  echo -n 'Do you want to convert to GIFA ?                (y/n) :'
  set conv=$<
else
  echo 'usage : ' $0 ' dest spectro user disk experiment expno conv-switch '
  exit
endif
if ($conv == 'y') then
  if ($#argv == 9) then
     set del=$8
     set trf=$9
  else
     echo -n 'Do you want to delete uxnmr data file after transfer? (y/n) :'
     set del=$<
     echo -n 'Do you want to realize the transfer? (n just converts) (y/n):'
     set trf=$<
  endif
else
  set del='n'
  set trf='y'
endif
if ($trf == 'y') then
   mkdir -p $localpath/$exp/$expno
   echo transfering /$disk/data/$user/nmr/$exp/$expno from guest@$spect
# REMARK !!!!
# this is SGI syntax !
# on certain machine, (eg HP) the following line sould be
#   rcp -r guest@$spect\:/$disk/data/$user/nmr/$exp/$expno $localpath/$exp/$expno
#
#    if ("`uname`" != "IRIX") then
#	echo WARNING Shell-Script might be buggy on non-SGI machines
#    endif
    rcp -r guest@$spect\:/$disk/data/$user/nmr/$exp/$expno $localpath/$exp
endif
if ($conv == 'y') then
     set direc = $localpath/$exp/$expno
     echo converting $direc
     if  ( -e $direc/ser ) then
     	if ( -e $direc/acqu3s ) then
     		set dim=3
     	else
        	set dim=2
        endif
     else if ( -e $direc/fid ) then
        set dim=1
     else
        echo "GETEXP : No data file found"
	set dim=0
     endif

	if ( $dim == 3 ) then
		echo "Warning, Choosing AQORDER = 3-2-1"
		echo "call ux2cach.sh directly if this does not fit your needs"
		ux2cach.sh $direc $direc/data $dim 1 0
	else
		ux2cach.sh $direc $direc/data $dim 1
	endif
     if ($del == 'y') then
       rm -rf $direc/ser $direc/fid $direc/pdata
     endif
endif
chmod -w  $localpath/$exp/$expno/*
echo done
exit
